<script src=lib4.js></script>

<body bgcolor=black text=white link=white alink=white vlink=white>
<center>
<canvas id='canvas1' width=600 height=600></canvas>
</center>
</body>


<!!-------- VERTEX SHADER: YOU PROBABLY DON'T WANT TO CHANGE THIS RIGHT NOW -------->

<script id='my_vertex_shader' type='x-shader/x-vertex'>
   attribute vec3 aPos;
   varying   vec3 vPos;
   void main() {
      gl_Position = vec4(aPos, 1.);
      vPos = aPos;
   }
</script>


<!!-------- FRAGMENT SHADER: THIS IS WHERE YOU WILL DO YOUR WORK -------->

<script id='my_fragment_shader' type='x-shader/x-fragment'>

 uniform float uTime;   // TIME, IN SECONDS

 const int nl = 2;
 const int ns = 2;

 uniform vec3 uLDir[nl]; // LIGHT DIRECTION
 uniform vec3 uLCol[nl]; // LIGHT COLOR

 uniform vec4 uSph[ns];   // THE SPHERE
 uniform mat4 uPhong[ns]; // PHONG PARAMETERS

 varying vec3 vPos;     // -1 < vPos.x < +1
                        // -1 < vPos.y < +1
                        //      vPos.z == 0

 // FOCAL LENGTH OF CAMERA ROUGHLY MIMICS A STANDARD 50mm LENS

 float f = 3.0;

 // FUNCTION TO INTERSECT A SPHERE WITH A RAY

 float raySphere(vec4 S, vec3 V, vec3 W) {
    V -= S.xyz;
    float vw = dot(V, W);
    float d = vw * vw - dot(V, V) + S.w * S.w;
    return d < 0. ? -1. : -vw - sqrt(d);
 }

 // CREATE PROCEDURAL SPHERE TEXTURES

 vec3 textureSphere(int ii, vec3 N) {

    // START WITH WHITE.

    vec3 color = vec3(1.,1.,1.);

    // MORE COMPLEX TEXTURE FOR THE PLANET.

    if (ii == 0) {

       // ANIMATED ROTATION OF THE SPHERE TEXTURE

       float theta = 0.2 * uTime;
       float x =  N.x * cos(theta) + N.z * sin(theta);
       float z = -N.x * sin(theta) + N.z * cos(theta);

       // FRACTAL NOISE-BASED TEXTURE TO DELINEATE LAND AND SEA

       float s = .5;
       s += .40 * noise( 2. * vec3(x,N.y,z));
       s += .20 * noise( 4. * vec3(x,N.y,z));
       s += .10 * noise( 8. * vec3(x,N.y,z));
       s += .05 * noise(16. * vec3(x,N.y,z));
       s = step(s, 0.4);

       // NOISE-BASED TEXTURE FOR THE LAND AREAS OF THE PLANET

       float m = .5 + .5 * noise(30. * vec3(x,N.y,z));
       color *= mix(vec3(.1,.9,.9), vec3(.9,.6,.3) * m, s);
    }

    // SIMPLE COHERENT NOISE TEXTURE FOR THE MOON.

    if (ii == 1) {
       color *= .8 + .5 * noise(5. * N);
    }

    return color;
 }

 // SHADE A SPHERE USING PHONG REFLECTION MODEL.

 vec3 shadeSphere(int ii, vec3 P, vec3 N, mat4 M, vec3 W) {

    // CALCULATE TEXTURE FOR THIS SPHERE

    vec3 texture = textureSphere(ii, N);

    // FETCH PHONG MATERIAL PROPERTIES.

    vec3 ambient = M[0].rgb;
    vec3 diffuse = M[1].rgb;
    vec4 specular = M[2];

    // START WITH JUST AMBIENT REFLECTION.

    vec3 color = ambient * texture;

    // LOOP THROUGH ALL LIGHT SOURCES.

    for (int i = 0 ; i < nl ; i++) {

       // SEE IF THE SURFACE POINT IS IN SHADOW FROM THIS LIGHT

       vec3 Vs = P + 0.001 * uLDir[i];  // RAY FROM OUTSIDE SURFACE
       vec3 Ws = uLDir[i];              // TOWARD LIGHT SOURCE.

       // BY SHOOTING A SHADOW RAY FOR THIS LIGHT TO EVERY SPHERE.

       float t = -1.;
       for (int j = 0 ; j < ns ; j++)
          t = max(t, raySphere(uSph[j], Vs, Ws));

       // IF THE SURFACE POINT IS NOT IN SHADOW FROM THIS LIGHT

       if (t < 0.) {

          // ADD DIFFUSE COMPONENT

          color += diffuse * max(0., dot(N, uLDir[i])) * uLCol[i] * texture;

          // ADD SPECULAR COMPONENT

          vec3 R = 2. * N * dot(N, uLDir[i]) - uLDir[i];
          color += specular.rgb * pow(max(0., dot(R, -W)), specular.a) * uLCol[i];

       }
    }
    return color;
 }

 void main() {

    // START WITH A DARK BLUE SKY.

    vec3 color = vec3(0., 0., 0.05);

    // TRACE A RAY AT THIS PIXEL DIRECTION.

    vec3 V = vec3(0., 0., f);
    vec3 W = normalize(vec3(vPos.x, vPos.y, -f));

    // FIRST DETERMINE WHICH SPHERE WE HIT, IF ANY.

    float t0 = 10000.; // HUGE STARTING VALUE FOR t.
    vec3 P, N;
    mat4 M;
    int ii = -1;

    // LOOP THROUGH ALL SPHERES.

    for (int i = 0 ; i < ns ; i++) {
       vec4 S = uSph[i];
       float t = raySphere(S, V, W);

       // IF THE RAY HITS THE SPHERE NEARER THAN ALL OTHERS

       if (t > 0. && t < t0) {
          t0 = t;
          ii = i;

          // COMPUTE SURFACE POSITION AND NORMAL DIRECTION.
   
          P = V + t * W;
          N = normalize(P - S.xyz);
          M = uPhong[i];
       }
    }

    // IF THE RAY HAS HIT AT LEAST ONE SPHERE

    if (ii >= 0) {

       // SHADE AND TEXTURE THE SPHERE.

       color = shadeSphere(ii, P, N, M, W);

       // FORM A MIRROR REFLECTION RAY.

       vec3 Wr = 2. * N * dot(N, -W) + W;
       vec3 Vr = P + .001 * Wr;

       // CHECK FOR MIRROR REFLECTION OF OTHER SPHERES.

       int jj = -1;
       vec3 Pr, Nr;
       mat4 Mr;

       float t0 = 1000.;
       for (int j = 0 ; j < ns ; j++) {
          float t = raySphere(uSph[j], Vr, Wr);
	  if (t > 0. && t < t0) {
	     t0 = t;
	     jj = j;
	     Pr = Vr + t * Wr;
	     Nr = normalize(Pr - uSph[j].xyz);
	     Mr = uPhong[j];
	  }
       }

       // IF THERE IS A MIRROR REFLECTION, MIX IT INTO COLOR.

       if (jj >= 0) {
          vec3 mirrorColor = shadeSphere(jj, Pr, Nr, Mr, Wr);
	  color = mix(color, mirrorColor, .5);
       }
    }

    // ADD COHERENT NOISE-BASED ANIMATED LAYERED FOG

    vec3 wind = vec3(-0.4 * uTime, 0., 0.);
    float fog = .7 + .6 * noise(1.5 * vPos + wind);
    float layer = 0.5 - 0.5 * vPos.y;
    fog *= 2. * layer * layer;
    color = mix(color, vec3(.1,.095,.09), fog);

    // SET THE FINAL COLOR FOR THE PIXEL

    gl_FragColor = vec4(sqrt(color), 1.0);
 }</script>


<!!-------- CREATE A PROPERLY DESCRIPTIVE TITLE BELOW -------->

<script id='my_title' type='text/html'>
RAY TRACING WITH TEXTURE, SHADOWS AND REFLECTION
</script>


<!!-------- HERE IS WHERE YOU CAN PROVIDE A LONGER TEXT DESCRIPTION -------->

<script id='my_instructions' type='text/html'><font color=#b0b0b0>
This is a simple ray tracer in a fragment<br>
shader that I wrote so the students in my<br>
computer graphics class can see how<br>
these various aspects of ray tracing<br>
can be implemented.
<p>
It includes two procedurally textured<br>
spheres with phong reflection model,<br>
shadows and mirror reflection and<br>
animated procedural atmosphere.
</script>


<!!-------- YOU PROBABLY DON'T WANT TO CHANGE ANYTHING BELOW FOR NOW -------->
   
<script>

// CREATE THE HTML DOCUMENT

let vs = my_vertex_shader.innerHTML,
    fs = my_fragment_shader.innerHTML;
    fs = fs.substring(1, fs.length);

document.body.innerHTML = [''
   ,'<center><font size=6 color=#b0b0b0>' + my_title.innerHTML + '</center>'
   ,'<TABLE cellspacing=0 cellpadding=0><TR>'
   ,'<td width=50></td><td><font color=red size=5><div id=errorMessage>&nbsp;</div></font></td>'
   ,'</TR><TR>'
   ,'<table cellspacing=10>'
   ,'<tr>'
   ,'<td valign=top><font size=2 color=red><div id=errorMarker>&nbsp;</div></font></td>'
   ,'<td valign=top>'
   ,'<textArea id=textArea spellcheck=false '
   ,'style="font:16px courier;outline-width:0;border-style:none;resize:none;overflow:scroll;"'
   ,'></textArea>'
   ,'</td><td valign=top>' + document.body.innerHTML + '</td>'
   ,'<td valign=top><font size=5>' + my_instructions.innerHTML + '</td>'
   ,'</tr></table>'
   ,'</TR></TABLE>'
   ].join('');

// SET UP THE EDITABLE TEXT AREA ON THE LEFT SIDE.

let text = fs.split('\n'), cols = 0;
for (let i = 0 ; i < text.length ; i++)
   cols = Math.max(cols, text[i].length);

textArea.rows = text.length;
textArea.cols = cols;
textArea.value = fs;
textArea.style.backgroundColor = '#202020';
textArea.style.color = 'white';

// REPARSE THE SHADER PROGRAM AFTER EVERY KEYSTROKE.

textArea.onkeyup = function() { canvas1.setShaders(vs, this.value); }

// SET THE CURRENT TIME IN SECONDS BEFORE RENDERING EACH FRAME.

let startTime = Date.now();

let dot = (a, b) => a[0]*b[0] + a[1]*b[1] + a[2]*b[2];
let norm = a => Math.sqrt(dot(a,a));
let normalize = a => { let s = norm(a); return [ a[0]/s,a[1]/s,a[2]/s ]; }

function animate(gl) {
   let time = (Date.now() - startTime) / 1000;

   setUniform('1f', 'uTime', time);

   // EACH LIGHT DIRECTION VECTOR MUST BE UNIT LENGTH.

   let r3 = 1 / Math.sqrt(3);

   let L0 = normalize([1,1,Math.sin(time)]);
   let L1 = normalize([-1,-1,-1]);

   setUniform('3fv', 'uLDir', L0.concat(L1));

   setUniform('3fv', 'uLCol', [ 1,1,1,      .5,.5,.5 ] );

   setUniform('Matrix4fv', 'uPhong', false, [
      .1,.1,.1,0,  .2,.2,.2,0,  .8,.8,.8,20,  0,0,0,0,
      .1,.05,.025,0,  .2,.1,.05,0,  .8,.4,.2,2,  0,0,0,0,
   ]);

   let x = .7 * Math.cos(time);
   let z = .7 * Math.sin(time);
   setUniform('4fv', 'uSph', [ 0,0,0,.4,
                               x,0,z,.2 ] );
}

// START EVERYTHING.

gl_start(canvas1, vs, fs);
</script>

