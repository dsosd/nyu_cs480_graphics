<script src=lib3.js></script>

<body bgcolor=black text=white link=white alink=white vlink=white>
<center>
<canvas id='canvas1' width=400 height=400></canvas>
</center>
</body>


<!!-------- VERTEX SHADER: YOU PROBABLY DON'T WANT TO CHANGE THIS RIGHT NOW -------->

<script id='my_vertex_shader' type='x-shader/x-vertex'>
   attribute vec3 aPos;
   varying   vec3 vPos;
   void main() {
      gl_Position = vec4(aPos, 1.);
      vPos = aPos;
   }
</script>


<!!-------- FRAGMENT SHADER: THIS IS WHERE YOU WILL DO YOUR WORK -------->

<script id='my_fragment_shader' type='x-shader/x-fragment'>

 uniform float uTime;   // TIME, IN SECONDS

 const int nl = 2;
 const int ns = 2;

 uniform vec3 uLDir[nl]; // LIGHT DIRECTION
 uniform vec3 uLCol[nl]; // LIGHT COLOR

 uniform vec4 uSph[ns]; // THE SPHERE

 varying vec3 vPos;     // -1 < vPos.x < +1
                        // -1 < vPos.y < +1
                        //      vPos.z == 0

 // FOCAL LENGTH OF CAMERA ROUGHLY MIMICS A STANDARD 50mm LENS

 float f = 3.0;

 // FUNCTION TO INTERSECT A SPHERE WITH A RAY

 float raySphere(vec4 S, vec3 V, vec3 W) {
    V -= S.xyz;
    float vw = dot(V, W);
    float d = vw * vw - dot(V, V) + S.w * S.w;
    return d < 0. ? -1. : -vw - sqrt(d);
 }

 void main() {

 // AN ORBITING SPHERE

 ///vec4 S = vec4(.5 * cos(2. * uTime), 0., .5 * sin(2. * uTime), 0.5);

 // A STATIONARY SPHERE AT THE ORIGIN

 ///vec4 S = vec4(0., 0., 0., 0.8);

 // THE LIGHT SOURCE DIRECTION

    vec3  L = uLDir[0];

 // START WITH A DARK BLUE SKY

    vec3 color = vec3(0., 0., 0.05);

 // TRACE A RAY AT THIS PIXEL DIRECTION

    vec3 V = vec3(0., 0., f);
    vec3 W = normalize(vec3(vPos.x, vPos.y, -f));

    // FIRST DETERMINE WHICH SPHERE WE HIT, IF ANY.

    float t0 = 10000.; // HUGE HONKING START VALUE OF t
    vec3 P, N;
    int ii = -1;

    for (int i = 0 ; i < ns ; i++) {
       vec4 S = uSph[i];
       float t = raySphere(S, V, W);

       // IF THE RAY HITS THE SPHERE

       if (t > 0. && t < t0) {
          t0 = t;
	  ii = i;

          // COMPUTE SURFACE POSITION AND NORMAL DIRECTION
   
          P = V + t * W;
          N = normalize(P - S.xyz);
       }
    }

    // IF THE RAY HAS HIT AT LEAST ONE SPHERE

    if (ii >= 0) {

       // SET PHONG MATERIAL PROPERTIES

       vec3 ambient = vec3(.1,.1,.1);
       vec3 diffuse = vec3(.8,.8,.8);

////// HERE YOU SHOULD ADD: vec4 specular = ...

       // START WITH AMBIENT COLOR OF THE OBJECT

       color = ambient;

       for (int i = 0 ; i < nl ; i++) {

	  // SEE IF THE SURFACE POINT IS IN SHADOW FROM THIS LIGHT

          vec3 Vs = P + 0.001 * uLDir[i];  // RAY FROM OUTSIDE SURFACE
          vec3 Ws = uLDir[i];              // TOWARD LIGHT SOURCE.

	  // BY SHOOTING A SHADOW RAY FOR THIS LIGHT TO EVERY SPHERE.

          float t = -1.;
          for (int j = 0 ; j < ns ; j++)
             t = max(t, raySphere(uSph[j], Vs, Ws));

	  // IF THE SURFACE POINT IS NOT IN SHADOW FROM THIS LIGHT

	  if (t < 0.) {

             // ADD DIFFUSE COMPONENT

             color += diffuse * max(0., dot(N, uLDir[i])) * uLCol[i];

             // ADD SPECULAR COMPONENT

/*********************************************************************

  For homework, you need to implement the specular component.

  You will need four numbers:

     3 numbers for the color of the specular highlight: red,green,blue
     1 number for the power of the highlight (higher is shinier): p

  You can store those four values in a vec4:

     vec4 specular = vec4(red,green,blue,p);

  For each light source not in shadow, you need to do the following:

     (1) Compute reflection vector R = 2 * dot(N,LDir) * N - LDir

     (2) Compute dot product d = dot(R,eyeDir).
         You can use -W for eyeDir, since it points back to the eye.

     (3) If d is positive, then add in the specular highlight:

           color += LCol * specular * pow(d, p)

*********************************************************************/

          }
       }
    }

// THEN DO SHADING ON THE SPHERE WE HIT, IF ANY.

    if (ii == 0) {

    // ANIMATED ROTATION OF THE SPHERE TEXTURE

       float theta = 0.2 * uTime;
       float x =  N.x * cos(theta) + N.z * sin(theta);
       float z = -N.x * sin(theta) + N.z * cos(theta);

    // FRACTAL NOISE-BASED TEXTURE TO DELINEATE LAND AND SEA

       float s = .5;
       s += .40 * noise( 2. * vec3(x,N.y,z));
       s += .20 * noise( 4. * vec3(x,N.y,z));
       s += .10 * noise( 8. * vec3(x,N.y,z));
       s += .05 * noise(16. * vec3(x,N.y,z));
       s = step(s, 0.4);

    // NOISE-BASED TEXTURE FOR THE LAND AREAS OF THE PLANET

       float m = .5 + .5 * noise(30. * vec3(x,N.y,z));
       color *= mix(vec3(.1,.9,.9), vec3(.9,.6,.3) * m, s);
    }

    if (ii == 1) {
       color *= .8 + .5 * noise(5. * N);
    }

 // NOISE-BASED ANIMATED LAYERED FOG

    vec3 wind = vec3(-0.4 * uTime, 0., 0.);
    float fog = .7 + .6 * noise(1.5 * vPos + wind);
    float layer = 0.5 - 0.5 * vPos.y;
    fog *= 2. * layer * layer;
    color = mix(color, vec3(.1,.095,.09), fog);

 // SET THE FINAL COLOR FOR THE PIXEL

    gl_FragColor = vec4(sqrt(color), 1.0);
 }</script>


<!!-------- CREATE A PROPERLY DESCRIPTIVE TITLE BELOW -------->

<script id='my_title' type='text/html'>
HERE YOU CAN GIVE YOUR PROGRAM A TITLE
</script>


<!!-------- HERE IS WHERE YOU CAN PROVIDE A LONGER TEXT DESCRIPTION -------->

<script id='my_instructions' type='text/html'><font color=#b0b0b0>
Here you can write a description<br>
in HTML format of what your program<br>
is doing, and why you wrote it.
<p>
<i>For example:</i>
<p>
This is a really simple fragment shader<br>
program I wrote so the students in my<br>
computer graphics class can see how<br>
<code>uTime</code> and <code>vPos</code> can be the inputs for<br>
a fragment shader, and <code>gl_FragColor</code><br>
is the output.
</script>


<!!-------- YOU PROBABLY DON'T WANT TO CHANGE ANYTHING BELOW FOR NOW -------->
   
<script>

// CREATE THE HTML DOCUMENT

let vs = my_vertex_shader.innerHTML,
    fs = my_fragment_shader.innerHTML;
    fs = fs.substring(1, fs.length);

document.body.innerHTML = [''
   ,'<center><font size=6 color=#b0b0b0>' + my_title.innerHTML + '</center>'
   ,'<TABLE cellspacing=0 cellpadding=0><TR>'
   ,'<td width=50></td><td><font color=red size=5><div id=errorMessage>&nbsp;</div></font></td>'
   ,'</TR><TR>'
   ,'<table cellspacing=10>'
   ,'<tr>'
   ,'<td valign=top><font size=2 color=red><div id=errorMarker>&nbsp;</div></font></td>'
   ,'<td valign=top>'
   ,'<textArea id=textArea spellcheck=false '
   ,'style="font:16px courier;outline-width:0;border-style:none;resize:none;overflow:scroll;"'
   ,'></textArea>'
   ,'</td><td valign=top>' + document.body.innerHTML + '</td>'
   ,'<td valign=top><font size=5>' + my_instructions.innerHTML + '</td>'
   ,'</tr></table>'
   ,'</TR></TABLE>'
   ].join('');

// SET UP THE EDITABLE TEXT AREA ON THE LEFT SIDE.

let text = fs.split('\n'), cols = 0;
for (let i = 0 ; i < text.length ; i++)
   cols = Math.max(cols, text[i].length);

textArea.rows = text.length;
textArea.cols = cols;
textArea.value = fs;
textArea.style.backgroundColor = '#202020';
textArea.style.color = 'white';

// REPARSE THE SHADER PROGRAM AFTER EVERY KEYSTROKE.

textArea.onkeyup = function() { canvas1.setShaders(vs, this.value); }

// SET THE CURRENT TIME IN SECONDS BEFORE RENDERING EACH FRAME.

let startTime = Date.now();

function animate(gl) {
   let time = (Date.now() - startTime) / 1000;

   setUniform('1f', 'uTime', time);

   // EACH LIGHT DIRECTION VECTOR MUST BE UNIT LENGTH.

   let r3 = 1 / Math.sqrt(3);

   setUniform('3fv', 'uLDir', [ r3,r3,r3,  -r3,-r3,r3] );

   setUniform('3fv', 'uLCol', [ 1,1,1,      .5,.5,.5 ] );

   let x = .7 * Math.cos(time);
   let z = .7 * Math.sin(time);
   setUniform('4fv', 'uSph', [ 0,0,0,.4,
                               x,0,z,.2 ] );
}

// START EVERYTHING.

gl_start(canvas1, vs, fs);
</script>

